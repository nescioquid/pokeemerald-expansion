const LOCALID_RIVAL = 15

mapscripts Route104South_MapScripts {
  MAP_SCRIPT_ON_FRAME_TABLE [
    VAR_BOARD_BRINEY_BOAT_STATE, 1: _Route104South_EventScript_StartSailToDewford
  ]
  MAP_SCRIPT_ON_TRANSITION: _Route104South_OnTransition
}

script _Route104South_EventScript_StartSailToDewford {
  lockall
  goto(Route104_EventScript_SailToDewford)
  end
}

script _Route104South_OnTransition {
  call(Common_EventScript_SetupRivalGfxId)
  call(_Route104South_EventScript_TrySetRivalPos)
  end
}

script _Route104South_EventScript_TrySetRivalPos {
  if (var(VAR_BOARD_BRINEY_BOAT_STATE) >= 1) {
    goto(Route104_EventScript_DontSetRivalPos)
  }
  if (flag(FLAG_MET_RIVAL_RUSTBORO)) {
    goto(Route104_EventScript_DontSetRivalPos)
  }
  if (!flag(FLAG_REGISTER_RIVAL_POKENAV)) {
    goto(Route104_EventScript_DontSetRivalPos)
  }
  setobjectxyperm(LOCALID_RIVAL, 17, 20)
  return
}

script _Route104South_EventScript_RivalTrigger {
  lockall
  setflag(FLAG_HIDE_RUSTBORO_CITY_RIVAL)
  setvar(VAR_RUSTBORO_CITY_STATE, 8)
  setvar(VAR_ROUTE104_STATE, 2)
  applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterUp)
  waitmovement(0)
  delay(10)
  addobject(LOCALID_RIVAL)
  clearflag(FLAG_HIDE_ROUTE_104_RIVAL)
  applymovement(OBJ_EVENT_ID_PLAYER, Route104_Movement_PlayerBackUp)
  waitmovement(0)
  applymovement(LOCALID_RIVAL, Route104_Movement_RivalExitBrineysCottage)
  waitmovement(0)
  delay(20)
  setvar(VAR_0x8008, 0)
  call(RustboroCity_EventScript_PlayRivalMusic)
  playse(SE_PIN)
  applymovement(LOCALID_RIVAL, Common_Movement_ExclamationMark)
  waitmovement(0)
  applymovement(LOCALID_RIVAL, Common_Movement_Delay48)
  waitmovement(0)
  checkplayergender
  switch (var(VAR_RESULT)) {
    case   MALE: goto(_Route104South_EventScript_MayEncounter)
    case FEMALE: goto(_Route104South_EventScript_BrendanEncounter)
  }
}

script _Route104South_EventScript_MayEncounter {
  if (flag(FLAG_DEFEATED_RIVAL_ROUTE_104)) {
    goto(Route104_EventScript_MayDefeated)
  }
  if (flag(FLAG_REGISTER_RIVAL_POKENAV)) {
    goto(Route104_EventScript_MayAskToBattle)
  }
  setflag(FLAG_REGISTER_RIVAL_POKENAV)
  msgbox(Route104_Text_MayWeShouldRegister, MSGBOX_DEFAULT)
  closemessage
  delay(30)
  playfanfare(MUS_REGISTER_MATCH_CALL)
  msgbox(Route104_Text_RegisteredMay, MSGBOX_DEFAULT)
  waitfanfare
  closemessage
  delay(30)
  setflag(FLAG_ENABLE_RIVAL_MATCH_CALL)
  applymovement(OBJ_EVENT_ID_PLAYER, Route104_Movement_PlayerFaceRival)
  applymovement(LOCALID_RIVAL, Route104_Movement_RivalWalkSlowLeft)
  msgbox(Route104_Text_MayHowsYourPokedex, MSGBOX_DEFAULT)
  closemessage
  waitmovement(0)
  applymovement(LOCALID_RIVAL, Route104_Movement_RivalApproachPlayer)
  waitmovement(0)
  copyobjectxytoperm(LOCALID_RIVAL)
  msgbox(Route104_Text_MayMinesDecentLetsBattle, MSGBOX_YESNO)
  goto_if_eq(VAR_RESULT, YES, Route104_EventScript_BattleMay)
  msgbox(Route104_Text_MayHaventRaisedPokemon, MSGBOX_DEFAULT)
  call(Route104_EventScript_RestoreMusic)
  releaseall
  end
}

script _Route104South_EventScript_BrendanEncounter {
  if (flag(FLAG_DEFEATED_RIVAL_ROUTE_104)) {
    goto(Route104_EventScript_BrendanDefeated)
  }
  if (flag(FLAG_REGISTER_RIVAL_POKENAV)) {
    goto(Route104_EventScript_BrendanAskToBattle)
  }
  setflag(FLAG_REGISTER_RIVAL_POKENAV)
  msgbox(Route104_Text_BrendanWeShouldRegister, MSGBOX_DEFAULT)
  closemessage
  delay(30)
  playfanfare(MUS_REGISTER_MATCH_CALL)
  msgbox(Route104_Text_RegisteredBrendan, MSGBOX_DEFAULT)
  waitfanfare
  closemessage
  delay(30)
  setflag(FLAG_ENABLE_RIVAL_MATCH_CALL)
  applymovement(OBJ_EVENT_ID_PLAYER, Route104_Movement_PlayerFaceRival)
  applymovement(LOCALID_RIVAL, Route104_Movement_RivalWalkSlowLeft)
  msgbox(Route104_Text_BrendanHowsYourPokedex, MSGBOX_DEFAULT)
  closemessage
  waitmovement(0)
  applymovement(LOCALID_RIVAL, Route104_Movement_RivalApproachPlayer)
  waitmovement(0)
  copyobjectxytoperm(LOCALID_RIVAL)
  msgbox(Route104_Text_BrendanDoingGreatLetsBattle, MSGBOX_YESNO)
  goto_if_eq(VAR_RESULT, YES, Route104_EventScript_BattleBrendan)
  msgbox(Route104_Text_BrendanNoConfidence, MSGBOX_DEFAULT)
  call(Route104_EventScript_RestoreMusic)
  releaseall
  end
}
