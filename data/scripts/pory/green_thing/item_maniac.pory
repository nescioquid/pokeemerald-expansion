// the Diving Treasure Hunter now gives HM08 Dive and is a Pearl maniac

script Pory_EventScript_TreasureHunter {
  lock
  faceplayer
  if (!flag(FLAG_MET_DIVING_TREASURE_HUNTER)) {
    msgbox(format("I'm the Diving Treasure Hunter!\pI'm the awesome dude who makes deep-sea dives to gather all the Pearls resting at the bottom.\pTell me, have you ever gone diving with your Pokémon?\pNo? That's a downer. Well, I'll tell you what. Take this!"))
    giveitem(ITEM_HM_DIVE)
    setflag(FLAG_RECEIVED_HM_DIVE)
    setflag(FLAG_OMIT_DIVE_FROM_STEVEN_LETTER)
    msgbox(format("It's the Hidden Machine Dive. While you're using Surf, you may notice darker patches of water.\pUse Dive in those places, and you'll drop to the seafloor. When you want to come back up, use Dive again. You won't be able to surface in some places, though."))
    msgbox(format("Hope you bring me back some sweet Pearls you find down there diving with your Pokémon, friend!"))
    setflag(FLAG_MET_DIVING_TREASURE_HUNTER)
    release
    end
  }
  checkitem(ITEM_BIG_PEARL)
  if (var(VAR_RESULT)) {
    checkitem(ITEM_PEARL)
    if (var(VAR_RESULT)) {
      random(2)
      switch (var(VAR_RESULT)) {
        case 0: call(Pory_EventScript_TreasureHunter_DoBigPearls)
        case 1: call(Pory_EventScript_TreasureHunter_DoPearls)
      }
    } else {
      call(Pory_EventScript_TreasureHunter_DoBigPearls)
    }
  } else {
    checkitem(ITEM_PEARL)
    if (var(VAR_RESULT)) {
      call(Pory_EventScript_TreasureHunter_DoPearls)
    } else {
      msgbox(format("Hope you bring me back some sweet Pearls you find down there diving with your Pokémon, friend!"))
    }
  }
  release
  end
}

script Pory_EventScript_TreasureHunter_DoBigPearls {
  showmoneybox(0, 0)
  checkitem(ITEM_BIG_PEARL, 2)
  if (var(VAR_RESULT)) {
    msgbox(format("Hey, you wanna give me one of those Big Pearls you got? How about ¥4000 for one?"), MSGBOX_YESNO)
  } else {
    msgbox(format("Hey, you wanna give me that Big Pearl you got? How about ¥4000 for it?"), MSGBOX_YESNO)
  }
  if (var(VAR_RESULT)) {
    msgbox(format("{PLAYER} handed the Big Pearl to the dude."))
    removeitem(ITEM_BIG_PEARL)
    addmoney(8000)
    updatemoneybox
    playse(SE_SHOP)
    msgbox(format("Narly bro, thanks!"))
  } else {
    msgbox(format("Man, that really harshes my mellow! Too bad!"))
  }
  hidemoneybox
}

script Pory_EventScript_TreasureHunter_DoPearls {
  showmoneybox(0, 0)
  checkitem(ITEM_PEARL, 2)
  if (var(VAR_RESULT)) {
    msgbox(format("Hey, you wanna give me one of those Pearls you got? How about ¥1000 for one?"), MSGBOX_YESNO)
  } else {
    msgbox(format("Hey, you wanna give me that Pearl you got? How about ¥1000 for it?"), MSGBOX_YESNO)
  }
  if (var(VAR_RESULT)) {
    msgbox(format("{PLAYER} handed the Pearl to the dude."))
    removeitem(ITEM_PEARL)
    addmoney(2000)
    updatemoneybox
    playse(SE_SHOP)
    msgbox(format("Narly bro, thanks!"))
  } else {
    msgbox(format("Totally uncool, dude! Maybe next time though!"))
  }
  hidemoneybox
}

// adds a maniac in Slateport who trades evo stones for fossils

raw`
.set LOCALID_EVO_STONE_MANIAC, 36
`

script Pory_EventScript_EvoStoneManiac {
  lock
  faceplayer
  msgbox(format("I like rocks…\nDo you have any cool rocks?\pI'm thinking about trading some of mine…"))
  checkitem(ITEM_HELIX_FOSSIL)
  if (var(VAR_RESULT)) {
    setvar(VAR_TEMP_3, ITEM_HELIX_FOSSIL)
    bufferitemname(STR_VAR_3, ITEM_HELIX_FOSSIL)
    goto(Pory_EventScript_EvoStoneManiac_GiveEvoStones)
  }
  checkitem(ITEM_DOME_FOSSIL)
  if (var(VAR_RESULT)) {
    setvar(VAR_TEMP_3, ITEM_DOME_FOSSIL)
    bufferitemname(STR_VAR_3, ITEM_DOME_FOSSIL)
    goto(Pory_EventScript_EvoStoneManiac_GiveEvoStones)
  }
  checkitem(ITEM_OLD_AMBER)
  if (var(VAR_RESULT)) {
    setvar(VAR_TEMP_3, ITEM_OLD_AMBER)
    bufferitemname(STR_VAR_3, ITEM_OLD_AMBER)
    goto(Pory_EventScript_EvoStoneManiac_GiveEvoStones)
  }
  checkitem(ITEM_ROOT_FOSSIL)
  if (var(VAR_RESULT)) {
    setvar(VAR_TEMP_3, ITEM_ROOT_FOSSIL)
    bufferitemname(STR_VAR_3, ITEM_ROOT_FOSSIL)
    goto(Pory_EventScript_EvoStoneManiac_GiveEvoStones)
  }
  checkitem(ITEM_CLAW_FOSSIL)
  if (var(VAR_RESULT)) {
    setvar(VAR_TEMP_3, ITEM_CLAW_FOSSIL)
    bufferitemname(STR_VAR_3, ITEM_CLAW_FOSSIL)
    goto(Pory_EventScript_EvoStoneManiac_GiveEvoStones)
  }
  release
  end
}

script Pory_EventScript_EvoStoneManiac_GiveEvoStones {
  if (var(VAR_RESULT)) {
    playse(SE_PIN)
    applymovement(LOCALID_EVO_STONE_MANIAC, Common_Movement_ExclamationMark)
    waitmovement(0)
    applymovement(LOCALID_EVO_STONE_MANIAC, Common_Movement_Delay48)
    waitmovement(0)
    if (var(VAR_TEMP_3) == ITEM_HELIX_FOSSIL) {
      msgbox(format("Hey, is that a {STR_VAR_3}?\nPraise Helix!\pDo you wanna trade something for it? I'll give you three of my rocks if you let me have it. I get to choose which ones though!"), MSGBOX_YESNO)
    } elif (var(VAR_TEMP_3) == ITEM_OLD_AMBER) {
      msgbox(format("Hey, is that an {STR_VAR_3}?\pDo you wanna trade something for it? I'll give you three of my rocks if you let me have it. I get to choose which ones though!"), MSGBOX_YESNO)
    } else {
      msgbox(format("Hey, is that a {STR_VAR_3}?\pDo you wanna trade something for it? I'll give you three of my rocks if you let me have it. I get to choose which ones though!"), MSGBOX_YESNO)
    }
    if (var(VAR_RESULT)) {
      msgbox(format("{PLAYER} handed the {STR_VAR_3} to the strange rock enthusiast."))
      msgbox(format("Yesssss! Another for my collection!"))
      removeitem(VAR_TEMP_3)
      random(4)
      switch (var(VAR_RESULT)) {
        case 0: giveitem(ITEM_FIRE_STONE)
        case 1: giveitem(ITEM_WATER_STONE)
        case 2: giveitem(ITEM_THUNDER_STONE)
        case 3: giveitem(ITEM_LEAF_STONE)
      }
      goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull)
      random(2)
      switch (var(VAR_RESULT)) {
        case 0: giveitem(ITEM_SUN_STONE)
        case 1: giveitem(ITEM_MOON_STONE)
      }
      goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull)
      random(4)
      switch (var(VAR_RESULT)) {
        case 0: giveitem(ITEM_SHINY_STONE)
        case 1: giveitem(ITEM_DAWN_STONE)
        case 2: giveitem(ITEM_DUSK_STONE)
        case 3: giveitem(ITEM_ICE_STONE)
      }
      goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull)
      msgbox(format("There you go! Bring me more of your rocks whenever you think you have something cool!")) 
    } else {
      msgbox(format("Aww that's too bad… Are you sure? I really wanted to trade you for it though!"))
    }
  }
  release
  end
}
