const LOCAL_ID_NURSE = VAR_0x800B
const SERVICE_CHARGE_AMOUNT = VAR_TEMP_1

script _Common_EventScript_PkmnCenterNurse {
  lock
  faceplayer
  call(_Common_EventScript_PkmnCenterNurse_CheckGoldCard)
  if (!flag(FLAG_NURSE_MENTIONS_GOLD_CARD)) {
    call(_Common_EventScript_PkmnCenterNurse_Hello)
    if (flag(FLAG_BADGE01_GET)) {
      call(_Common_EventScript_PkmnCenterNurse_ServiceCharge)
    }
  }
  call(_Common_EventScript_PkmnCenterNurse_HealPkmn)
  call(_Common_EventScript_PkmnCenterNurse_Goodbye)
  release
  end
}

script _Common_EventScript_PkmnCenterNurse_CheckGoldCard {
  if (!flag(FLAG_NURSE_MENTIONS_GOLD_CARD)) {
    specialvar(VAR_RESULT, CountPlayerTrainerStars)
    if (var(VAR_RESULT) == 4) {
      msgbox(gText_WelcomeCutShort)
      playse(SE_PIN)
      applymovement(LOCAL_ID_NURSE, Common_Movement_ExclamationMark)
      waitmovement(0)
      applymovement(LOCAL_ID_NURSE, Common_Movement_Delay48)
      waitmovement(0)
      setflag(FLAG_NURSE_MENTIONS_GOLD_CARD)
      msgbox(gText_NoticesGoldCard, MSGBOX_YESNO)
      if (!var(VAR_RESULT)) {
        call(_Common_EventScript_PkmnCenterNurse_Goodbye)
        release
        end
      }
    }
  } else {
    msgbox(gText_YouWantTheUsual, MSGBOX_YESNO)
    if (!var(VAR_RESULT)) {
      call(_Common_EventScript_PkmnCenterNurse_Goodbye)
      release
      end
    }
  }
}

script _Common_EventScript_PkmnCenterNurse_HealPkmn {
  incrementgamestat(GAME_STAT_USED_POKECENTER)
  call(_Common_EventScript_PkmnCenterNurse_IllTakeYourPkmn)
  waitmessage
  applymovement(LOCAL_ID_NURSE, Common_Movement_WalkInPlaceFasterLeft)
  waitmovement(0)
  dofieldeffect(FLDEFF_POKECENTER_HEAL)
  waitfieldeffect(FLDEFF_POKECENTER_HEAL)
  applymovement(LOCAL_ID_NURSE, Common_Movement_WalkInPlaceFasterDown)
  waitmovement(0)
  special(HealPlayerParty)
  call(_Common_EventScript_PkmnCenterNurse_CheckPokerus)
  call(_Common_EventScript_PkmnCenterNurse_RestoredPkmnToFullHealth)
  waitmessage
  applymovement(LOCAL_ID_NURSE, Movement_PkmnCenterNurse_Bow)
  waitmovement(0)
  call(_Common_EventScript_PkmnCenterNurse_CheckPlayerWaitingInUnionRoom)
}

script _Common_EventScript_PkmnCenterNurse_CheckPokerus {
  if (!flag(FLAG_POKERUS_EXPLAINED)) {
    specialvar(VAR_RESULT, IsPokerusInParty)
    if (var(VAR_RESULT)) {
      message(gText_PokerusExplanation)
      setflag(FLAG_POKERUS_EXPLAINED)
    }
  }
}

script _Common_EventScript_PkmnCenterNurse_CheckPlayerWaitingInUnionRoom {
  
  specialvar(VAR_RESULT, PlayerNotAtTrainerHillEntrance)
  if (var(VAR_RESULT)) {
    specialvar(VAR_RESULT, BufferUnionRoomPlayerName)
    copyvar(VAR_0x8008, VAR_RESULT)
    if (var(VAR_RESULT)) {
      if (!flag(FLAG_NURSE_UNION_ROOM_REMINDER)) {
        msgbox(CableClub_Text_PlayerIsWaiting)
        setflag(FLAG_NURSE_UNION_ROOM_REMINDER)
      }
    }
  }
}

script _Common_EventScript_PkmnCenterNurse_ServiceCharge {
  showmoneybox(0, 0)
  call(_Common_EventScript_GetBadgeCount)
  switch(var(VAR_RESULT)) {
    case 0: setvar(SERVICE_CHARGE_AMOUNT, 1000)
    case 1: setvar(SERVICE_CHARGE_AMOUNT, 1000)
    case 2: setvar(SERVICE_CHARGE_AMOUNT, 2000)
    case 3: setvar(SERVICE_CHARGE_AMOUNT, 3000)
    case 4: setvar(SERVICE_CHARGE_AMOUNT, 4000)
    case 5: setvar(SERVICE_CHARGE_AMOUNT, 5000)
    case 6: setvar(SERVICE_CHARGE_AMOUNT, 6000)
    case 7: setvar(SERVICE_CHARGE_AMOUNT, 7000)
    case 8: setvar(SERVICE_CHARGE_AMOUNT, 8000)
  }
  buffernumberstring(STR_VAR_1, SERVICE_CHARGE_AMOUNT)
  random(3)
  switch (var(VAR_RESULT)) {
    case 0: msgbox(format("We now charge a modest fee of ¥{STR_VAR_1} for our services, ¥100 for each Gym Badge you have.\nIs that ok?"), MSGBOX_YESNO)
    case 1: msgbox(format("That'll be ¥{STR_VAR_1}, ok?"), MSGBOX_YESNO)
    case 2: msgbox(format("Our services now cost ¥100 per Badge obtained, so that'll be ¥{STR_VAR_1} today. Would you like us to heal your Pokémon?"), MSGBOX_YESNO)
  }
  if (var(VAR_RESULT)) {
  call(_Common_EventScript_GetBadgeCount)
  switch(var(VAR_RESULT)) {
    case 0: checkmoney(1000)
    case 1: checkmoney(1000)
    case 2: checkmoney(2000)
    case 3: checkmoney(3000)
    case 4: checkmoney(4000)
    case 5: checkmoney(5000)
    case 6: checkmoney(6000)
    case 7: checkmoney(7000)
    case 8: checkmoney(8000)
  }
  if (!var(VAR_RESULT)) {
    msgbox(format("I'm sorry, but that's unfortunately not enough."))
    hidemoneybox
    release
    end
  }
  call(_Common_EventScript_GetBadgeCount)
  switch(var(VAR_RESULT)) {
    case 0: removemoney(1000)
    case 1: removemoney(1000)
    case 2: removemoney(2000)
    case 3: removemoney(3000)
    case 4: removemoney(4000)
    case 5: removemoney(5000)
    case 6: removemoney(6000)
    case 7: removemoney(7000)
    case 8: removemoney(8000)
  }
    updatemoneybox
    msgbox(format("Thank you!"))
    hidemoneybox
  } else {
    msgbox(format("Ok! Maybe next time then."))
    hidemoneybox
    release
    end
  }
}

script _Common_EventScript_PkmnCenterNurse_Hello {
  if (flag(FLAG_SYS_GAME_CLEAR)) {
    random(8)
    switch (var(VAR_RESULT)) {
      case 0: msgbox(format("{FONT_SMALL}…is …is that the league champion?!"))
      case 1: msgbox(format("{FONT_SMALL}…I'm pretty sure that's the champion!"))
    }
  }
  if (!flag(FLAG_BADGE01_GET)) {
    random(3)
    switch (var(VAR_RESULT)) {
      case 0: msgbox(format("Hello, and welcome to the Pokémon Center."))
      case 1: msgbox(format("Hello, and welcome to the Pokémon Center."))
      case 2: msgbox(format("Hi! Welcome to our center!"))
    }
  } else {
    random(12)
    switch (var(VAR_RESULT)) {
      case  0: msgbox(format("Hello, and welcome to the Pokémon Center."))
      case  1: msgbox(format("Hello, and welcome to the Pokémon Center."))
      case  2: msgbox(format("Hello, and welcome to the Pokémon Center."))
      case  3: msgbox(format("Hello, and welcome to the Pokémon Center."))
      case  4: msgbox(format("Hi! Welcome to our center!"))
      case  5: msgbox(format("Hi! Welcome to our center!"))
      case  6: msgbox(format("Hi! Welcome to our center!"))
      case  7: msgbox(format("Hello, hello! Back again?"))
      case  8: msgbox(format("Hello, hello! Back again?"))
      case  9: msgbox(format("Hey! How's it going?"))
      case 10: msgbox(format("Hey! How's it going?"))
      case 11: msgbox(format("{FONT_SMALL}…sheesh, again already? seriously?… {FONT_NORMAL}Hello! Welcome!"))
      // case 12: msgbox(format("Hey… You do realize that this is free healthcare, right?"))
    }
  }
}

script _Common_EventScript_PkmnCenterNurse_IllTakeYourPkmn {
  random(3)
  switch (var(VAR_RESULT)) {
    case 0: message(format("I'll take your Pokémon for a few seconds…"))
    case 1: message(format("Let me see your Pokémon for just a moment…"))
    case 2: message(format("I'll have these back to you in no time…"))
  }
}

script _Common_EventScript_PkmnCenterNurse_RestoredPkmnToFullHealth {
  random(4)
  switch (var(VAR_RESULT)) {
    case 0: message(format("Thank you for waiting."))
    case 1: message(format("We've restored your Pokémon to full health."))
    case 2: message(format("Thanks for waiting!"))
    case 3: message(format("Your Pokémon look as happy and healthy as ever!"))
  }
}

script _Common_EventScript_PkmnCenterNurse_Goodbye {
  if (!flag(FLAG_BADGE01_GET)) {
    random(3)
    switch (var(VAR_RESULT)) {
      case 0: msgbox(format("Until next time!"))
      case 1: msgbox(format("See you later!"))
      case 2: msgbox(format("Goodbye!"))
    }
  } else {
    random(9)
    switch (var(VAR_RESULT)) {
      case 0: msgbox(format("Until next time!"))
      case 1: msgbox(format("See you later!"))
      case 2: msgbox(format("Goodbye!"))
      case 3: msgbox(format("Bye bye!"))
      case 4: msgbox(format("Bye now!"))
      case 5: msgbox(format("Bye!"))
      case 6: msgbox(format("Ciao!"))
      case 7: msgbox(format("Good luck out there!"))
      case 8: msgbox(format("Have fun!"))
    }
  }
}
