script _Route104_PrettyPetalFlowerShop_OnTransition {
  setflag(FLAG_LANDMARK_FLOWER_SHOP)
  if (!flag(FLAG_MET_PRETTY_PETAL_SHOP_OWNER)) {
    setobjectxyperm(LOCALID_FLOWER_SHOP_OWNER, 4, 6)
    setflag(FLAG_TEMP_1)
  }
  if (!flag(FLAG_RECEIVED_WAILMER_PAIL)) {
    setobjectxyperm(LOCALID_FLOWER_SHOP_WAILMER_PAIL_GIRL, 7, 3)
    setobjectmovementtype(LOCALID_FLOWER_SHOP_WAILMER_PAIL_GIRL, MOVEMENT_TYPE_WANDER_LEFT_AND_RIGHT)
    setflag(FLAG_TEMP_2)
  }
  if (!flag(FLAG_BADGE03_GET)) {
    setobjectxyperm(LOCALID_FLOWER_SHOP_OWNER, 4, 6)
    setobjectxyperm(LOCALID_FLOWER_SHOP_WAILMER_PAIL_GIRL, 7, 3)
    setobjectmovementtype(LOCALID_FLOWER_SHOP_WAILMER_PAIL_GIRL, MOVEMENT_TYPE_WANDER_LEFT_AND_RIGHT)
    setflag(FLAG_TEMP_1)
    setflag(FLAG_TEMP_2)
  }
  end
}

script _Route104_PrettyPetalFlowerShop_EventScript_RandomBerryGirl {
  lock
  faceplayer
  dotimebasedevents
  if (!flag(FLAG_DAILY_FLOWER_SHOP_RECEIVED_BERRY)) {
    msgbox(format("I'm trying to be like my big sisters. I'm growing flowers, too!\pHere you go! They're for you!"))
    if (!flag(FLAG_BADGE02_GET)) {
      call(_Common_EventScript_GiveBerry_Status)
    } else {
      call(_Common_EventScript_GiveBerry_Generic)
    }
    setflag(FLAG_DAILY_FLOWER_SHOP_RECEIVED_BERRY)
  }
  msgbox(format("You can plant a Berry and grow it big, or you can make a Pokémon hold it.\pBut now they have a machine that mixes up different Berries and makes candies for Pokémon.\pI want some candy, too."))
  release
  end
}

script _Route104_PrettyPetalFlowerShop_EventScript_ShopOwner {
  lock
  faceplayer
  if (flag(FLAG_TEMP_1)) {
    if (!flag(FLAG_MET_PRETTY_PETAL_SHOP_OWNER)) {
      msgbox(Route104_PrettyPetalFlowerShop_Text_ThisIsPrettyPetalFlowerShop)
      msgbox(Route104_PrettyPetalFlowerShop_Text_IntroLearnAboutBerries, MSGBOX_YESNO)
      if (var(VAR_RESULT)) {
        call(Route104_PrettyPetalFlowerShop_EventScript_ExplainBerries)
      } else {
        call(Route104_PrettyPetalFlowerShop_EventScript_DontExplainBerries)
      }
      setflag(FLAG_MET_PRETTY_PETAL_SHOP_OWNER)
    } else {
      goto(Route104_PrettyPetalFlowerShop_EventScript_AlreadyMet)
    }
    } else {
      goto(Route104_PrettyPetalFlowerShop_EventScript_SellDecorations)
    }
  release
  end
}

script _Route104_PrettyPetalFlowerShop_EventScript_WailmerPailGirl {
  lock
  faceplayer
  if (flag(FLAG_TEMP_2)) {
    if (!flag(FLAG_RECEIVED_WAILMER_PAIL)) {
      goto(Route104_PrettyPetalFlowerShop_EventScript_GiveWailmerPail)
    } else {
      msgbox(Route104_PrettyPetalFlowerShop_Text_WailmerPailExplanation)
    }
  } else {
    message(format("Been watering your Berries like I taught you? How can I help you?"))
    waitmessage
    pokemart(_Route104_PrettyPetalFlowerShop_Pokemart_Mulches)
    msgbox(format("Until next time!"))
  }
  release
  end
}

mart _Route104_PrettyPetalFlowerShop_Pokemart_Mulches {
  ITEM_GROWTH_MULCH
  ITEM_STABLE_MULCH
  ITEM_GOOEY_MULCH
  ITEM_DAMP_MULCH
}
