const PUSH_TO_DYNMULTISTACK_IF_OWNED = VAR_TEMP_8
const PEARL_CHOSEN = VAR_TEMP_1

script _Route124_DivingTreasureHuntersHouse_EventScript_TreasureHunter {
  lock
  faceplayer
  if (!flag(FLAG_MET_DIVING_TREASURE_HUNTER)) {
    msgbox(format("I'm the Diving Treasure Hunter!\pI'm the awesome dude who makes deep-sea dives to gather all the Pearls resting at the bottom.\pTell me, have you ever gone diving with your Pokémon?\pNo? That's a downer. Well, I'll tell you what. Take this!"))
    giveitem(ITEM_HM_DIVE)
    setflag(FLAG_RECEIVED_HM_DIVE)
    setflag(FLAG_OMIT_DIVE_FROM_STEVEN_LETTER)
    msgbox(format("That's the Hidden Machine Dive. While you're using Surf, you may notice darker patches of water.\pUse Dive in those places, and you'll drop to the seafloor. When you want to come back up, use Dive again. You won't be able to surface in some places, though.\pHope you bring me back some sweet Pearls you find down there diving with your Pokémon.\pRemember, I pay cash!"))
    setflag(FLAG_MET_DIVING_TREASURE_HUNTER)
    release
    end
  }
  checkitem(ITEM_PEARL)
  if (!var(VAR_RESULT)) {
    checkitem(ITEM_BIG_PEARL)
    if (!var(VAR_RESULT)) {
      msgbox(format("Hope you bring me back some sweet Pearls you find down there diving with your Pokémon, dude!"))
      release
      end
    }
  }
  showmoneybox(0, 0)
  message(format("Hey, you got any Pearls you wanna sell? I pay cash!"))
  waitmessage
  setvar(PUSH_TO_DYNMULTISTACK_IF_OWNED, ITEM_PEARL)
  call(_Common_EventScript_PushItemToDynmultistackIfOwned)
  setvar(PUSH_TO_DYNMULTISTACK_IF_OWNED, ITEM_BIG_PEARL)
  call(_Common_EventScript_PushItemToDynmultistackIfOwned)
  dynmultistack(0, 4, FALSE, 4, FALSE, 0, DYN_MULTICHOICE_CB_SHOW_ITEM)
  if (var(VAR_RESULT) == MULTI_B_PRESSED) {
    msgbox(format("Totally uncool, dude! Maybe next time though!"))
    hidemoneybox
    release
    end
  }
  copyvar(PEARL_CHOSEN, VAR_RESULT)
  switch (var(PEARL_CHOSEN)) {
    case ITEM_PEARL:
      msgbox(format("How about ¥2000 for it?"), MSGBOX_YESNO)
    case ITEM_BIG_PEARL:
      msgbox(format("How about ¥8000 for it?"), MSGBOX_YESNO)
  }
  if (!var(VAR_RESULT)) {
    msgbox(format("Totally uncool, dude! Maybe next time though!"))
    hidemoneybox
    releaseall
    end
  }
  switch (var(PEARL_CHOSEN)) {
    case ITEM_PEARL:
      msgbox(format("{PLAYER} handed the Pearl to the dude."))
      removeitem(ITEM_PEARL)
      addmoney(2000)
    case ITEM_BIG_PEARL:
      msgbox(format("{PLAYER} handed the Big Pearl to the dude."))
      removeitem(ITEM_BIG_PEARL)
      addmoney(8000)
  }
  updatemoneybox
  playse(SE_SHOP)
  msgbox(format("Narly bro, thanks!"))
  hidemoneybox
  release
  end
}

script _Route124_DivingTreasureHuntersHouse_EventScript_ShardTradeBoard {
  msgbox(format("A sign that looks like it should say\n“We accept Visa” but actually used to be a menu of different items for trade."), MSGBOX_SIGN)
  release
  end
}
