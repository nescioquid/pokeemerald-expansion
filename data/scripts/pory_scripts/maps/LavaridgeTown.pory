script _LavaridgeTown_EventScript_GiveTMSwordsDance {
  lock
  faceplayer
  if (!flag(FLAG_RECEIVED_TM_SWORDS_DANCE)) {
    msgbox(format("Dance and soul make power!\pSoul is way of karate for me!\pYou understand, yes?"), MSGBOX_YESNO)
    if (var(VAR_RESULT)) {
      msgbox(format("Ok! Ok!\nYou good kid!"))
    } else {
      msgbox(format("Oh…\nI so sad and wretched…\pYou take this and you understand way of karate of me, fool Trainer!"))
      giveitem(ITEM_TM_SWORDS_DANCE)
      goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull)
      setflag(FLAG_RECEIVED_TM_SWORDS_DANCE)
    }
  } else {
    msgbox(format("You understand way of karate now?"))
  }
  release
  end
}

script _LavaridgeTown_EventScript_EggWoman {
  lock
  faceplayer
  if (!flag(FLAG_RECEIVED_LAVARIDGE_EGG)) {
    msgbox(LavaridgeTown_Text_HaveEggWillYouTakeIt, MSGBOX_YESNO)
    if (!var(VAR_RESULT)) {
      msgbox(LavaridgeTown_Text_AsYouWishThen)
      release
      end
    }
    getpartysize
    if (var(VAR_RESULT) == PARTY_SIZE) {
      msgbox(LavaridgeTown_Text_NoRoomForThisEgg)
      release
      end
    }
    msgbox(LavaridgeTown_Text_HopeYoullWalkPlentyWithEgg)
    setflag(FLAG_RECEIVED_LAVARIDGE_EGG)
    playfanfare(MUS_OBTAIN_ITEM)
    message(LavaridgeTown_Text_ReceivedTheEgg)
    waitfanfare
    random(3)
    switch (var(VAR_RESULT)) {
      case 0:giveegg(SPECIES_TOGEPI)
      case 1:giveegg(SPECIES_WYNAUT)
      case 2:
        // stores the result (MALE (0) or FEMALE (1)) in VAR_RESULT
        checkplayergender
        switch (var(VAR_RESULT)) {
          case   MALE: giveegg(SPECIES_TOGEPI)
          case FEMALE: giveegg(SPECIES_WYNAUT)
        }
    }
  } else {
    msgbox(LavaridgeTown_Text_EverySoOftenEggFoundAtDayCare)
  }
  release
  end
}
