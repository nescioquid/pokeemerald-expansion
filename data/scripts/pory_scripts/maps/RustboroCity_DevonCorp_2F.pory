const GIVEMON_HELPER_ARTICLE = VAR_TEMP_9
const PUSH_TO_DYNMULTISTACK_IF_OWNED = VAR_TEMP_8

script _RustboroCity_DevonCorp_2F_EventScript_FossilScientist {
  lock
  faceplayer
  msgbox(format("I've been trying to develop a device that resurrects Pokémon from fossils…\pAnd, it's working!"))
  checkitem(ITEM_HELIX_FOSSIL)
  if (!var(VAR_RESULT)) {
    checkitem(ITEM_DOME_FOSSIL)
    if (!var(VAR_RESULT)) {
      checkitem(ITEM_OLD_AMBER)
      if (!var(VAR_RESULT)) {
        checkitem(ITEM_ROOT_FOSSIL)
        if (!var(VAR_RESULT)) {
          checkitem(ITEM_CLAW_FOSSIL)
          if (!var(VAR_RESULT)) {
            release
            end
          }
        }
      }
    }
  }
  if (!flag(FLAG_TEMP_1)) {
    message(format("Do you happen to have any? I can bring them back to life with my regenerator!"))
  } else {
    message(format("Do you have another fossil for me?"))
  }
  waitmessage
  setvar(PUSH_TO_DYNMULTISTACK_IF_OWNED, ITEM_HELIX_FOSSIL)
  call(_Common_EventScript_PushItemToDynmultistackIfOwned)
  setvar(PUSH_TO_DYNMULTISTACK_IF_OWNED, ITEM_DOME_FOSSIL)
  call(_Common_EventScript_PushItemToDynmultistackIfOwned)
  setvar(PUSH_TO_DYNMULTISTACK_IF_OWNED, ITEM_OLD_AMBER)
  call(_Common_EventScript_PushItemToDynmultistackIfOwned)
  setvar(PUSH_TO_DYNMULTISTACK_IF_OWNED, ITEM_ROOT_FOSSIL)
  call(_Common_EventScript_PushItemToDynmultistackIfOwned)
  setvar(PUSH_TO_DYNMULTISTACK_IF_OWNED, ITEM_CLAW_FOSSIL)
  call(_Common_EventScript_PushItemToDynmultistackIfOwned)
  dynmultistack(0, 0, FALSE, 6, FALSE, 0, DYN_MULTICHOICE_CB_SHOW_ITEM)
  if (var(VAR_RESULT) == MULTI_B_PRESSED) {
    msgbox(format("Oh, is that so?\pOur technological expertise here at Devon is outstanding, I tell you."))
    release
    end
  }
  copyvar(VAR_WHICH_FOSSIL_REVIVED, VAR_RESULT)
  bufferitemname(STR_VAR_1, VAR_WHICH_FOSSIL_REVIVED)
  if (var(VAR_WHICH_FOSSIL_REVIVED) == ITEM_HELIX_FOSSIL) {
    msgbox(format("Praise Helix! Let's do it right now.\p{PLAYER} handed the {STR_VAR_1} to the Devon researcher."))
  } else {
    msgbox(format("Excellent! Let's do it right away.\p{PLAYER} handed the {STR_VAR_1} to the Devon researcher."))
  }
  removeitem(VAR_WHICH_FOSSIL_REVIVED)
  msgbox(format("… … … … … … … …\n… … … … … … … …\p… … … … … … … …\n… … … … … … … …\pThanks for waiting! Your fossil came back to life!"))
  switch (var(VAR_WHICH_FOSSIL_REVIVED)) {
    case ITEM_HELIX_FOSSIL:
      setvar(VAR_TEMP_TRANSFERRED_SPECIES, SPECIES_OMANYTE)
      setvar(GIVEMON_HELPER_ARTICLE, 2)
      givemon(SPECIES_OMANYTE, 25)
    case ITEM_DOME_FOSSIL:
      setvar(VAR_TEMP_TRANSFERRED_SPECIES, SPECIES_KABUTO)
      setvar(GIVEMON_HELPER_ARTICLE, 1)
      givemon(SPECIES_KABUTO, 25)
    case ITEM_OLD_AMBER:
      setvar(VAR_TEMP_TRANSFERRED_SPECIES, SPECIES_AERODACTYL)
      setvar(GIVEMON_HELPER_ARTICLE, 2)
      givemon(SPECIES_AERODACTYL, 25)
    case ITEM_ROOT_FOSSIL:
      setvar(VAR_TEMP_TRANSFERRED_SPECIES, SPECIES_LILEEP)
      setvar(GIVEMON_HELPER_ARTICLE, 1)
      givemon(SPECIES_LILEEP, 25)
    case ITEM_CLAW_FOSSIL:
      setvar(VAR_TEMP_TRANSFERRED_SPECIES, SPECIES_ANORITH)
      setvar(GIVEMON_HELPER_ARTICLE, 2)
      givemon(SPECIES_ANORITH, 25)
  }
  call(_Common_EventScript_GivemonHelper)
  setflag(FLAG_RECEIVED_REVIVED_FOSSIL_MON)
  setflag(FLAG_TEMP_1)
  msgbox(format("Come back with more fossils!"))
  release
  end
}
