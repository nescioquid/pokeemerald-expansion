script Common_PoryScript_MoveReminder {
  lock
  faceplayer
  if (!flag(FLAG_TEMP_1)) {
    msgbox(format("I'm a Move Reminder.\pI know all the moves that Pokémon learn--every one of them--and I can make Pokémon remember those moves.\pI can do it with some of yours if you'd like."), MSGBOX_YESNO)
    setflag(FLAG_TEMP_1)
  } else {
    msgbox(format("You want me to remind one of them?"), MSGBOX_YESNO)
  }
  if (var(VAR_RESULT)) {
    call(Common_PoryScript_MoveReminder_TeachMove)
  }
  msgbox(format("If your Pokémon ever need to remember some moves, you know where to find me!"))
  release
  end
}

script Common_PoryScript_MoveReminder_TeachMove {
  if (!flag(FLAG_TEMP_2)) {
    msgbox(format("Which one needs reminding?"))
    setflag(FLAG_TEMP_2)
  } else {
    msgbox(format("Any others?"))
  }
  special(ChooseMonForMoveRelearner)
  waitstate
  if (var(VAR_0x8004) != PARTY_NOTHING_CHOSEN) {
    special(IsSelectedMonEgg)
    if (var(VAR_RESULT)) {
      msgbox(format("Huh? There isn't a single move that I can teach an Egg."))
      call(Common_PoryScript_MoveReminder_TeachMove)
    }
    if (var(VAR_0x8005) == 0) {
      msgbox(format("Sorry… It doesn't appear as if there's any move I can help that Pokémon remember."))
      call(Common_PoryScript_MoveReminder_TeachMove)
    }
    msgbox(format("What move should I remind them of?"))
    special(TeachMoveRelearnerMove)
    waitstate
    call(Common_PoryScript_MoveReminder_TeachMove)
  } else {
    clearflag(FLAG_TEMP_2)
    msgbox(format("Come back if your Pokémon ever need help remembering moves!"))
    release
    end
  }
}
